# Maintainer:  Tim Stahlhut <stahta01@gmail.com>

_realname=cc65
_sourcedir=${_realname}-git
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.19.r1017.ad7c5a661
pkgrel=1
pkgdesc="C compiler for 6502 family microprocessors (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
options=('!strip') 
license=('BSD')
makedepends=('git'
             "${MINGW_PACKAGE_PREFIX}-cc")
url='https://cc65.github.io/cc65/'
source=(${_sourcedir}::git+https://github.com/cc65/cc65)
sha512sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_sourcedir}

  printf "%s" "$(git describe --tags --long --first-parent \
                 | sed 's/^V//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd ${srcdir}/${_sourcedir}

  git clean -fxd
}

build() {
  cd ${srcdir}/${_sourcedir}

  make PREFIX=${MINGW_PREFIX}
}

package() {
  cd ${srcdir}/${_sourcedir}

  make DESTDIR="$pkgdir" PREFIX=${MINGW_PREFIX} install
  install -Dm644 "LICENSE" "$pkgdir${MINGW_PREFIX}/share/licenses/$_realname/license"
}
