# Maintainer: Tim Stahlhut <stahta01@gmail.com>

#_do_MAINTAIN_PNGS=no # Not needed anymore

_realname=kicad
# GCC 12.1 silent build error bug was avoided by using wxWidgets 3.2 with STL configuration
#_git_commit=cd8e464339bca217247e51fafc1d6aa3c79260cc # 5.99.0.r12833 fails to set default ucrt64 config
#_git_commit=e867a4fd27fba140ea90266d373ba20b4768331a # 5.99.0.r12832 works to set default ucrt64 config
_wx_basever=3.2
_sourcedir=${_realname}-git
pkgbase=mingw-w64-${_realname}-git
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=6.99.0.r2821.g2ee65b2d83
pkgrel=1
pkgdesc="Software for the creation of electronic schematic diagrams and printed circuit board artwork (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clang32' 'mingw32')
url="https://www.kicad.org/"
license=("GPLv3+")
groups=("${MINGW_PACKAGE_PREFIX}-eda")
options=('!strip')
#if [[ "${MSYSTEM}" == "UCRT64" ]]; then
  #options+=('debug')
#else
  options+=('!debug')
#fi
depends=(
  "${MINGW_PACKAGE_PREFIX}-boost"
  "${MINGW_PACKAGE_PREFIX}-curl"
  "${MINGW_PACKAGE_PREFIX}-glew"
  "${MINGW_PACKAGE_PREFIX}-glm"
# 'mingw32' gives "cc1plus.exe: out of memory allocating 65536 bytes"
# on sim_model_ngspice_data.cpp file.
  $([[ ${MSYSTEM} != MINGW32 ]] && echo "${MINGW_PACKAGE_PREFIX}-ngspice")
  "${MINGW_PACKAGE_PREFIX}-python"
#  "${MINGW_PACKAGE_PREFIX}-wxPython"
  "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-libs"
  "${MINGW_PACKAGE_PREFIX}-openssl"
  "${MINGW_PACKAGE_PREFIX}-freeglut"
  "${MINGW_PACKAGE_PREFIX}-zlib"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-aarch64* ]] && echo "${MINGW_PACKAGE_PREFIX}-opencascade")
  "${MINGW_PACKAGE_PREFIX}-cairo"
  #$([[ ${_do_MAINTAIN_PNGS} == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-inkscape")
  #$([[ ${_do_MAINTAIN_PNGS} == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-pngcrush")
)
makedepends=(
  'git' 'make' 'patch'
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-doxygen"
  "${MINGW_PACKAGE_PREFIX}-pkg-config"
  "${MINGW_PACKAGE_PREFIX}-swig"
  "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw"
)
source=(
  '002-ki-6.99-cmake-fixes-for-MINGW-CLANG.patch'
  '003-ki-6.0-code-fixes-for-GNUC-CLANG.patch'
  '005-ki-6.0-Skip-ffloat-store-under-CLANG.patch'
  '006-ki-6.0-rewrite-kiwin32_rc_for_clang.patch'
  '007-ki-6.0-manifest-remove-win10-11-manifest-support.patch'
#  "${_sourcedir}"::"git+https://github.com/KiCad/kicad-source-mirror.git#commit=${_git_commit}"
  "${_sourcedir}"::"git+https://github.com/KiCad/kicad-source-mirror.git"
)
sha256sums=('6aed28addf1aac14acbb770bd18d6e3a0767ebb31b128b1a3f38e4a07ee21a25'
            'd8d5f4bdd0aa6d8a907710c523f6f95840636cb2ef69e5275c6ed4966f134353'
            'f9cd1cbbafe98a76e0bd7cb61ab76b05b5a2c22848dd5ea8042c06e0c4864288'
            'e03dbb58409145c8fb54991d8259f14bc0ba6b21abca24f9b914ec354c9df89c'
            'f6c99f22082aa5368a5d53a882763d4aefb0c8257373f7be7d8929f403d16bf7'
            'SKIP')
_doc=("ca" "de" "en" "es" "fr" "id" "it" "ja" "nl" "pl" "ru" "zh")
_sub=("footprints" "symbols" "templates" "packages3D")

pkgver() {
  cd ${srcdir}/${_sourcedir}
  ( set -o pipefail
    _tag=$( head -n 200 cmake/KiCadVersion.cmake | grep 'KICAD_SEMANTIC_VERSION "' | sed -e 's/KICAD_SEMANTIC_VERSION//' | sed -e 's/-unknown//' | sed -e 's/set//' | tr -d '() "' )
    _version=$( echo ${_tag} | tr -d '-' )
    printf "%s.r%s.g%s" "${_version}" $(git rev-list --count ${_tag}..HEAD) $(git rev-parse --short HEAD)
  )
}

prepare() {
  cd ${srcdir}/${_sourcedir}

  # The next three patch files are needed to get CLANG64/32 to build
  patch -Np1 -i "${srcdir}"/002-ki-6.99-cmake-fixes-for-MINGW-CLANG.patch
  patch -Np1 -i "${srcdir}"/003-ki-6.0-code-fixes-for-GNUC-CLANG.patch
  patch -Np1 -i "${srcdir}"/006-ki-6.0-rewrite-kiwin32_rc_for_clang.patch
  patch -Np1 -i "${srcdir}"/005-ki-6.0-Skip-ffloat-store-under-CLANG.patch
  # This patch allows me to test program using my old Intel GPU driver under win10
  patch -Np1 -i "${srcdir}"/007-ki-6.0-manifest-remove-win10-11-manifest-support.patch
}

build() {
  cd "${srcdir}"
  msg2 "Build kicad"
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  # Possible CI Server and local build error fix.
  export pythonLocation="${MINGW_PREFIX}/bin/python"

  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=( -DCMAKE_BUILD_TYPE=Debug )
  else
    extra_config+=( -DCMAKE_BUILD_TYPE=Release )
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-aarch64* ]]; then
    extra_config+=( -DOCC_INCLUDE_DIR=${MINGW_PREFIX}/include/opencascade )
  fi

  #if [[ ${_do_MAINTAIN_PNGS} == "yes" ]]; then
    #extra_config+=( -DMAINTAIN_PNGS=ON )
  #fi

  #if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
    #export CXXFLAGS="-pipe"
    #extra_config+=( -DCMAKE_CXX_FLAGS_RELEASE:STRING="-O0 -DNDEBUG" )
  #fi

  #extra_config+=( -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON )

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;DCMAKE_PREFIX_PATH=;DEFAULT_INSTALL_PATH=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "MSYS Makefiles" \
    -DCMAKE_PREFIX_PATH=${MINGW_PREFIX} \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "${extra_config[@]}" \
    -DDEFAULT_INSTALL_PATH=${MINGW_PREFIX} \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config-${_wx_basever} \
    -DKICAD_USE_OCC="$( [[ ${MINGW_PACKAGE_PREFIX} == *-clang-aarch64* ]] && echo "OFF" || echo "ON" )" \
    -DKICAD_SPICE="$( [[ ${MSYSTEM} == MINGW32 ]] && echo "OFF" || echo "ON" )" \
    -DKICAD_SCRIPTING_WXPYTHON=OFF \
    -DKICAD_BUILD_QA_TESTS=OFF \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python.exe \
    -DPYTHON_INCLUDE_DIR=$(${MINGW_PREFIX}/bin/python.exe -c "from sysconfig import get_paths as gp; print(gp()[\"include\"])") \
    -DPYTHON_ROOT_DIR=${MINGW_PREFIX} \
    -DKICAD_BUILD_I18N=ON \
    ../${_sourcedir}

  "${MINGW_PREFIX}"/bin/cmake.exe --build . # --parallel 1 --verbose
}

for _doclang in ${_doc[@]}; do _doc_pkg+=("\"${MINGW_PACKAGE_PREFIX}-${_realname}-doc-${_doclang}: Documentation for KiCad (mingw-w64)\""); done
for _feature in ${_sub[@]}; do _sub_pkg+=("\"${MINGW_PACKAGE_PREFIX}-${_realname}-${_feature}\""); done

eval "package_${MINGW_PACKAGE_PREFIX}-${_realname}-git() {

  msg2 \"Package kicad\"
  optdepends+=(${_doc_pkg[@]} ${_sub_pkg[@]})
  conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname})
  provides=(${MINGW_PACKAGE_PREFIX}-${_realname})

  cd \${srcdir}/build-${MSYSTEM}
  DESTDIR=\${pkgdir} \${MINGW_PREFIX}/bin/cmake.exe --install .
}"
